plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'com.jfrog.bintray' version '1.8.4'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
	id 'org.springframework.boot' version '2.2.6.RELEASE'
}

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	api 'com.redislabs:lettusearch:1.13.2'
	api 'org.apache.commons:commons-pool2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'com.redislabs:spring-redisearch'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/RediSearch/spring-redisearch.git'
	    version {
	        gpg {
	            sign = true
	        }
	        mavenCentralSync {
	            sync = true
	            user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USER')
	            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
	        }
	    }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.redislabs'
            artifactId 'spring-redisearch'

            pom {
                name = 'Spring RediSearch'
                packaging = 'jar'
                description = 'Spring-enabled client for RediSearch based on LettuSearch'
                url = 'https://github.com/RediSearch/spring-redisearch'

		        scm {
		          connection = 'scm:git:git://github.com/RediSearch/spring-redisearch.git'
		          developerConnection = 'scm:git:git@github.com:RediSearch/spring-redisearch.git'
		          url = 'https://github.com/RediSearch/spring-redisearch'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

group = 'com.redislabs'
description = 'Spring RediSearch'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task printVersion {
    doLast {
        println project.version
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "RediSearch"
    repo "spring-redisearch"
}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload
